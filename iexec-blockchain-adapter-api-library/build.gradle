plugins {
    id 'java-library'
    id 'io.freefair.lombok'
    id 'jacoco'
    id 'maven-publish'
}

dependencies {
    implementation platform('org.springframework.boot:spring-boot-dependencies:3.0.13')
    implementation "com.iexec.commons:iexec-commons-poco:$iexecCommonsPocoVersion"
    implementation "com.iexec.common:iexec-common:$iexecCommonVersion"
    implementation 'org.apache.commons:commons-lang3:3.12.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

testing {
    suites {
        test {
            useJUnitJupiter()
            dependencies {
                implementation 'org.junit.jupiter:junit-jupiter'
                implementation 'org.mockito:mockito-junit-jupiter'
            }
        }
    }
}

tasks.withType(Test).configureEach {
    finalizedBy jacocoTestReport
}

// sonarqube code coverage requires jacoco XML report
jacocoTestReport {
    reports {
        xml.required = true
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username nexusUser
                password nexusPassword
            }
            url = project.hasProperty("nexusUrl") ? project.nexusUrl : ''
        }
    }
}
