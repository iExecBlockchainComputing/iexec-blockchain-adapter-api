plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.10.2'
    id 'org.springframework.boot' version '3.3.8'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
    id 'org.sonarqube' version '5.1.0.4882'
    id 'maven-publish'
}

ext {
    testContainersVersion = '1.20.4'
}

if (!project.hasProperty('gitBranch')) {
    ext.gitBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
}

allprojects {
    group = 'com.iexec.blockchain'
    if (gitBranch != 'main' && gitBranch != 'master' && !(gitBranch ==~ '(release|hotfix|support)/.*')) {
        version += '-NEXT-SNAPSHOT'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        // iExec
        maven {
            url "https://docker-regis-adm.iex.ec/repository/maven-public/"
            credentials {
                username nexusUser
                password nexusPassword
            }
        }
        maven {
            url "https://jitpack.io"
        }
    }
    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add('-parameters')
}

dependencies {
    // Spring framework
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Spring Doc
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

    // iexec
    implementation "com.iexec.commons:iexec-commons-poco:$iexecCommonsPocoVersion"
    implementation "com.iexec.common:iexec-common:$iexecCommonVersion"

    // observability
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    // integration tests
    implementation project(':iexec-blockchain-adapter-api-library')
}

springBoot {
    buildInfo()
}

tasks.named("bootJar") {
    manifest {
        attributes("Implementation-Title": "iExec Blockchain Adapter API",
                "Implementation-Version": project.version)
    }
}

testing {
    suites {
        configureEach {
            dependencies {
                implementation 'org.awaitility:awaitility'
                implementation 'org.springframework.boot:spring-boot-starter-test'
                implementation 'org.springframework.security:spring-security-test'
                implementation "org.testcontainers:junit-jupiter:$testContainersVersion"
            }
        }
        test {
            useJUnitJupiter()
            dependencies {
                implementation "org.testcontainers:mongodb:$testContainersVersion"
            }
        }
        itest(JvmTestSuite) {
            dependencies {
                implementation project()
                implementation project(':iexec-blockchain-adapter-api-library')
                implementation "com.iexec.commons:iexec-commons-poco:$iexecCommonsPocoVersion"
                implementation "com.iexec.common:iexec-common:$iexecCommonVersion"
                implementation 'org.apache.commons:commons-lang3'
                implementation 'org.hibernate.validator:hibernate-validator'
            }
        }
    }
}

tasks.withType(Test).configureEach {
    finalizedBy jacocoTestReport
    systemProperty "mongo.image", "mongo:7.0.15-jammy"
}

// sonarqube code coverage requires jacoco XML report
jacocoTestReport {
    reports {
        xml.required = true
    }
}
tasks.sonarqube.dependsOn tasks.jacocoTestReport

publishing {
    publications {
        maven(MavenPublication) {
            artifact tasks.named("bootJar")
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username nexusUser
                password nexusPassword
            }
            url = project.hasProperty("nexusUrl") ? project.nexusUrl : ''
        }
    }
}

ext.jarPathForOCI = relativePath(tasks.bootJar.outputs.files.singleFile)
ext.gitShortCommit = 'git rev-parse --short=8 HEAD'.execute().text.trim()
ext.ociImageName = 'local/' + ['bash', '-c', 'basename $(git config --get remote.origin.url) .git'].execute().text.trim()

tasks.register('buildImage', Exec) {
    group 'Build'
    description 'Builds an OCI image from a Dockerfile.'
    dependsOn bootJar
    commandLine 'docker', 'build', '--build-arg', 'jar=' + jarPathForOCI, '-t', ociImageName + ':dev', '.'
}
