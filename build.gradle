plugins {
	id 'java'
	id 'org.springframework.boot' version '2.4.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'jacoco'
	id 'org.sonarqube' version '3.3'
	id 'maven-publish'
}

ext {
	openFeignVersion = '11.6'
}

group = 'com.iexec.blockchain'
sourceCompatibility = '11'
targetCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenLocal()
	mavenCentral()
	// iExec
	maven {
		url "https://nexus.iex.ec/repository/maven-public/"
	}
	maven {
		url "https://jitpack.io"
	}
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
	integrationTestImplementation.extendsFrom testImplementation
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.awaitility:awaitility:4.0.1'

	implementation "io.springfox:springfox-boot-starter:3.0.0"
	implementation "io.springfox:springfox-swagger-ui:3.0.0"
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// iexec
	implementation "com.iexec.common:iexec-common:$iexecCommonVersion"

	// web3j bug required
	// NoSuchMethodError: 'okhttp3.RequestBody okhttp3.RequestBody.create(java.lang.String, okhttp3.MediaType)'
	implementation 'com.squareup.okhttp3:okhttp:4.3.1' // Web3j issue: https://github.com/web3j/web3j/issues/1180
	// NoSuchMethodError: 'byte[] kotlin.collections.ArraysKt.copyInto(byte[], byte[], int, int, int)'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.50' // https://stackoverflow.com/a/57907899

	// feign
	implementation "io.github.openfeign:feign-core:$openFeignVersion"
	implementation "io.github.openfeign:feign-jackson:$openFeignVersion"
	implementation "io.github.openfeign:feign-slf4j:$openFeignVersion"
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir 'src/itest/java'
		}
		resources.srcDir 'src/itest/resources'
	}
}

task itest(type: Test) {
	setTestClassesDirs(sourceSets.integrationTest.output)
	classpath = sourceSets.integrationTest.runtimeClasspath
	useJUnitPlatform()
}

jar {
	enabled = true
	archiveClassifier.set('library')
	from sourceSets.main.allSource
	duplicatesStrategy = 'exclude'
}

publishing {
	publications {
		maven(MavenPublication) {
			artifact bootJar
			from components.java
		}
	}
	repositories {
		maven {
			credentials {
				username project.hasProperty("nexusUser")? project.nexusUser: ''
				password project.hasProperty("nexusPassword")? project.nexusPassword: ''
			}
			url = project.hasProperty("nexusUrl")? project.nexusUrl: ''
		}
	}
}

test {
	useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.7"
}
// sonarqube code coverage requires jacoco XML report
jacocoTestReport {
    reports {
        xml.enabled true
    }
}
tasks.test.finalizedBy    tasks.jacocoTestReport
tasks.sonarqube.dependsOn tasks.jacocoTestReport

String gitBranch = "git rev-parse --abbrev-ref HEAD".execute().text.trim()
String gitShortCommit = "git rev-parse --short=8 HEAD".execute().text.trim()
String imageName   = 'nexus.iex.ec/iexec-blockchain-adapter-api'

ext.bootJarPath = relativePath(tasks.bootJar.outputs.files.singleFile)
ext.ociImageNameFull = ""

if (gitBranch.equals('master') || gitBranch.startsWith('release/') || gitBranch.startsWith('hotfix/')) {
    ext.ociImageNameFull = imageName + ':' + version
}
if (gitBranch.equals('develop')) {
    ext.ociImageNameFull = imageName + ':dev-' + gitShortCommit
}
if (gitBranch.startsWith('feature/') || gitBranch.startsWith('bugfix/')) {
    ext.ociImageNameFull = imageName + ':feature-' + gitShortCommit
}

ext.ociImageNameDev         = imageName + ':dev'
ext.ociImageNameShortCommit = imageName + ':' + gitShortCommit

task buildImage(type: Exec) {
    dependsOn tasks.bootJar
    description 'Build OCI image'
    commandLine("sh", "-c", "docker build -f docker/Dockerfile-local --build-arg spring_boot_jar=$bootJarPath -t $ociImageNameFull ."
            + " && docker tag $ociImageNameFull $ociImageNameDev")
}
